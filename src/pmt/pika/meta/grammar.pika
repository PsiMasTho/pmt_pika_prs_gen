// - COMMENTS AND WHITESPACE -
$newline = 16#A;
$whitespace = [" ", 10#9];
$single_line_comment = "//" $single_line_comment_rhs;
$single_line_comment_rhs = $newline | any $single_line_comment_rhs | eof;
$multi_line_comment = "/*" $multi_line_comment_rhs;  
$multi_line_comment_rhs = "*/" | any $multi_line_comment_rhs;  
$comment = $single_line_comment | $multi_line_comment;
$blank<hide = true> = ($comment | $whitespace | $newline)*;

// - KEYWORDS -
$kw_any<id = "Any">         = "any";
$kw_eof<id = "Eof">         = "eof";
$kw_epsilon<id = "Epsilon"> = "epsilon";

$kw_parameter_merge<hide = true>         = "merge";
$kw_parameter_unpack<hide = true>        = "unpack";
$kw_parameter_hide<hide = true>          = "hide";
$kw_parameter_hide_if_empty<hide = true> = "hide_if_empty";
$kw_parameter_id<hide = true>            = "id";
$kw_parameter_display_name<hide = true>  = "display_name";

// - SYMBOLS -
$comma<id    = "Comma", merge = true>    = ",";
$plus<id     = "Plus", merge = true>     = "+";
$star<id     = "Star", merge = true>     = "*";
$question<id = "Question", merge = true> = "?";
$tilde<hide  = true>                     = "~";
$exclamation<hide = true>                = "!";
$ampersand<hide = true>                  = "&";

// - LITERALS -
$string_middle = [10#32, 10#33, 10#35-10#126];

$string_literal<id = "StringLiteral", merge = true>       = 10#34~ $string_middle* 10#34~;
$character_literal<id = "CharacterLiteral", merge = true> = 10#34~ $string_middle 10#34~;
$integer_literal_lhs                                      = ["0"-"9"]+;
$integer_literal_rhs                                      = ["A"-"Z", "a"-"z", "0"-"9"]+;
$integer_literal<id   = "IntegerLiteral", merge = true>   = $integer_literal_lhs "#" $integer_literal_rhs;
$id_string_middle                                         = ["A"-"Z", "a"-"z", "_"] ["A"-"Z", "a"-"z", "0"-"9", "_"]*;
$id_string_literal<id = "IdStringLiteral", merge = true>  = 10#34~ $id_string_middle 10#34~;
$boolean_literal<id   = "BooleanLiteral", merge = true>   = "true" | "false";
$identifier_rhs                                           = "__"! ["A"-"Z", "a"-"z", "_"] ["A"-"Z", "a"-"z", "0"-"9", "_"]*; 
$identifier<id        = "Identifier", merge = true>       = ("$"~) $identifier_rhs;

// - PARAMETERS -
$parameter_id<id = "ParameterId", merge = true>                     = $kw_parameter_id                    $blank "="~ $blank $id_string_literal;
$parameter_displayname<id = "ParameterDisplayName", merge = true>   = $kw_parameter_display_name          $blank "="~ $blank $string_literal;
$parameter_merge<id = "ParameterMerge", merge = true>               = $kw_parameter_merge                 $blank "="~ $blank $boolean_literal;
$parameter_unpack<id = "ParameterUnpack", merge = true>             = $kw_parameter_unpack                $blank "="~ $blank $boolean_literal;
$parameter_hide<id = "ParameterHide", merge = true>                 = $kw_parameter_hide                  $blank "="~ $blank $boolean_literal;
$parameter_hide_if_empty<id = "ParameterHideIfEmpty", merge = true> = $kw_parameter_hide_if_empty         $blank "="~ $blank $boolean_literal;

$parameter<unpack = true> = $parameter_id | $parameter_displayname | $parameter_merge | $parameter_unpack | $parameter_hide | $parameter_hide_if_empty;
$parameter_list<unpack = true> = $blank $parameter ($blank ","~ $blank $parameter)* $blank;

// - GRAMMAR -
$grammar<unpack = true> = $blank $production ($blank $production)* $blank;

// - PRODUCTION -
$production<id = "Production"> = $identifier $blank "="~ $blank $definition $blank ";"~ |
                                 $identifier $blank "<"~ $parameter_list ">"~ $blank "="~ $blank $definition ";"~;

// - DEFINITION -
$definition<id = "Definition"> = $choices;

// - CHOICES -
$choices<id = "Choices"> = $sequence ($blank "|"~ $blank $sequence)* $blank;

// - SEQUENCE -
$sequence<id = "Sequence"> = $expression ($blank $expression)*;

// - EXPRESSION -
$expression<unpack = true> = $hidden_expression             |
                             $repetition_expression         |
                             $negative_lookahead_expression |
                             $positive_lookahead_expression |
                             $plain_expression;

$hidden_expression<id = "Hidden"> = $expression_body $tilde; 

$repetition_expression<id = "Repetition"> = $expression_body $repetition_postfix;

$negative_lookahead_expression<id = "NegativeLookahead"> = $expression_body $exclamation;

$positive_lookahead_expression<id = "PositiveLookahead"> = $expression_body $ampersand;

$plain_expression<unpack = true> = $expression_body;

$expression_body<unpack = true > = $identifier               |
                                   $string_literal           |
                                   $integer_literal          |
                                   $charset                  |
                                   "("~ $blank $choices ")"~ |
                                   $kw_epsilon               |
                                   $kw_eof                   |
                                   $kw_any;

// - REPETITION_POSTFIX -
$repetition_postfix<unpack = true> = $question |
                                     $star     |
                                     $plus     |
                                     $repetition_range;

$repetition_range<id = "RepetitionRange"> = "{"~ $blank ($integer_literal $blank)? $comma $blank ($integer_literal $blank)? "}"~ |
                                            "{"~ $blank $integer_literal $blank "}"~;

// - CHARSET -
$charset<id = "Charset"> = "["~ $blank $charset_item ($blank ","~ $blank $charset_item)* $blank "]"~;

$charset_item<unpack = true> = $charset_range |
                               $charset_literal;

$charset_range<id = "CharsetRange"> = $charset_literal $blank "-"~ $blank $charset_literal;

$charset_literal<unpack = true> = $character_literal |
                                  $integer_literal;